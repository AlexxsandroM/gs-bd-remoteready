
-- =============================================
-- 08_ADMIN_POST_AND_READ_GS.sql
-- Regras: Apenas ADMIN publica; leitura de post concede certificado (>=10)
-- =============================================

CREATE OR REPLACE TRIGGER TRG_CHK_TB_GS_BLOG_POST_ADMIN
BEFORE INSERT ON TB_GS_BLOG_POST
FOR EACH ROW
DECLARE
  v_role TB_GS_USUARIO.TP_ROLE%TYPE;
BEGIN
  SELECT TP_ROLE INTO v_role
  FROM TB_GS_USUARIO
  WHERE ID_USUARIO = :NEW.ID_USUARIO_CRIADOR;

  IF v_role <> 'ADMIN' THEN
    RAISE_APPLICATION_ERROR(-20901, 'Apenas ADMIN pode criar post.');
  END IF;
END;
/
CREATE TABLE TB_GS_POST_LEITURA (
  ID_LEITURA   NUMBER(10)       CONSTRAINT PK_TB_GS_POST_LEITURA PRIMARY KEY,
  ID_USUARIO   NUMBER(6)        CONSTRAINT FK_TB_GS_PL_USR  REFERENCES TB_GS_USUARIO(ID_USUARIO),
  ID_POST      NUMBER(6)        CONSTRAINT FK_TB_GS_PL_POST REFERENCES TB_GS_BLOG_POST(ID_POST),
  DT_LEITURA   DATE DEFAULT SYSDATE
);
ALTER TABLE TB_GS_POST_LEITURA
  ADD CONSTRAINT UQ_TB_GS_POST_LEITURA UNIQUE (ID_USUARIO, ID_POST);
CREATE INDEX IX_TB_GS_PL_USR  ON TB_GS_POST_LEITURA(ID_USUARIO);
CREATE INDEX IX_TB_GS_PL_POST ON TB_GS_POST_LEITURA(ID_POST);
CREATE SEQUENCE SEQ_TB_GS_POST_LEITURA START WITH 1 INCREMENT BY 1 NOCACHE;

CREATE OR REPLACE TRIGGER TRG_AUD_TB_GS_POST_LEITURA
AFTER INSERT OR DELETE ON TB_GS_POST_LEITURA
FOR EACH ROW
BEGIN
  IF INSERTING THEN
    INSERT INTO TB_GS_AUDITORIA
      (ID_AUDITORIA, NM_USUARIO, TP_OPERACAO, NM_TABELA, ID_REGISTRO, OLD_VALUES, NEW_VALUES, DT_OPERACAO)
    VALUES
      (SEQ_TB_GS_AUDITORIA.NEXTVAL, USER, 'INSERT', 'TB_GS_POST_LEITURA',
       :NEW.ID_LEITURA, NULL,
       'USR='||:NEW.ID_USUARIO||';POST='||:NEW.ID_POST, SYSDATE);
  ELSIF DELETING THEN
    INSERT INTO TB_GS_AUDITORIA
      (ID_AUDITORIA, NM_USUARIO, TP_OPERACAO, NM_TABELA, ID_REGISTRO, OLD_VALUES, NEW_VALUES, DT_OPERACAO)
    VALUES
      (SEQ_TB_GS_AUDITORIA.NEXTVAL, USER, 'DELETE', 'TB_GS_POST_LEITURA',
       :OLD.ID_LEITURA,
       'USR='||:OLD.ID_USUARIO||';POST='||:OLD.ID_POST, NULL, SYSDATE);
  END IF;
END;
/
CREATE OR REPLACE FUNCTION FN_QTD_POSTS_LIDOS(p_id_usuario IN NUMBER)
RETURN NUMBER IS
  v_qtd NUMBER;
BEGIN
  SELECT COUNT(*) INTO v_qtd
  FROM TB_GS_POST_LEITURA
  WHERE ID_USUARIO = p_id_usuario;
  RETURN v_qtd;
END;
/
CREATE OR REPLACE PROCEDURE PRC_AVALIAR_CERTIFICADO_LEITURA(p_id_usuario IN NUMBER) AS
  v_qtd NUMBER;
  v_existe NUMBER;
BEGIN
  v_qtd := FN_QTD_POSTS_LIDOS(p_id_usuario);
  IF v_qtd >= 10 THEN
    SELECT COUNT(*) INTO v_existe
    FROM TB_GS_CERTIFICADO
    WHERE ID_USUARIO = p_id_usuario
      AND UPPER(DS_TITULO) = 'LEITOR 10+';
    IF v_existe = 0 THEN
      PRC_INS_CERTIFICADO(p_id_usuario, 'Leitor 10+');
    END IF;
  END IF;
END;
/
CREATE OR REPLACE PROCEDURE PRC_REGISTRAR_LEITURA(
  p_id_usuario IN NUMBER,
  p_id_post    IN NUMBER
) AS
BEGIN
  INSERT INTO TB_GS_POST_LEITURA (ID_LEITURA, ID_USUARIO, ID_POST, DT_LEITURA)
  VALUES (SEQ_TB_GS_POST_LEITURA.NEXTVAL, p_id_usuario, p_id_post, SYSDATE);
EXCEPTION
  WHEN DUP_VAL_ON_INDEX THEN
    NULL;
END;
/
CREATE OR REPLACE PROCEDURE PRC_REGISTRAR_LEITURA_E_AVALIAR(
  p_id_usuario IN NUMBER,
  p_id_post    IN NUMBER
) AS
BEGIN
  PRC_REGISTRAR_LEITURA(p_id_usuario, p_id_post);
  PRC_AVALIAR_CERTIFICADO_LEITURA(p_id_usuario);
END;
/
